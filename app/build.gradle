apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply from: "$project.rootDir/jacoco.gradle"

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.thiosin.cryptoinfo"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'com.thiosin.cryptoinfo.ApplicationTestRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding = true
    }

    // Flavors
    def environment = "environment"
    flavorDimensions environment

    productFlavors {
        dev {
            dimension environment
            buildConfigField 'String', 'COINLIB_BASE_URL', "\"http://coinlib.io/api/v1/\""
            buildConfigField 'String', 'COINLIB_API_KEY', apikeyProperties['COINLIB_API_KEY']
            buildConfigField 'String', 'COINLAYER_BASE_URL', "\"http://api.coinlayer.com/api/\""
            buildConfigField 'String', 'COINLAYER_API_KEY', apikeyProperties['COINLAYER_API_KEY']
        }
        mock {
            dimension environment
        }
    }
}

dependencies {

    // Rainbow Cake
    def rainbow_cake_version = '1.2.0'
    implementation "co.zsmb:rainbow-cake-core:$rainbow_cake_version"
    implementation "co.zsmb:rainbow-cake-dagger:$rainbow_cake_version"
    implementation "co.zsmb:rainbow-cake-timber:$rainbow_cake_version"

    // Room
    def room_version = '2.2.5'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Navigation Components
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // AppCompat
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // KTX
    implementation 'androidx.core:core-ktx:1.3.2'

    // Material Design Components
    implementation 'com.google.android.material:material:1.2.1'

    // Layouts
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Coroutines
    def coroutines_version = '1.4.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Dagger
    def dagger_version = '2.29.1'
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // ThreeTenABP
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'

    // Krate
    implementation 'hu.autsoft:krate:0.4.0'

    // Moshi
    def moshi_version = '1.11.0'
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // OkHttp
    def okhttp_version = '4.9.0'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Glide
    def glide_version = '4.11.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Analytics
    implementation 'com.google.firebase:firebase-analytics:18.0.0'

    // Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:17.2.2'

    // Testing

    // Test Framework
    testImplementation 'junit:junit:4.13.1'

    // Rainbow Cake - ViewModel testing
    testImplementation "co.zsmb:rainbow-cake-test:$rainbow_cake_version"

    // MockK - Mocks
    testImplementation 'io.mockk:mockk:1.10.2'

    // Google Truth - Asserts
    testImplementation 'com.google.truth:truth:1.1'

    // AndroidX Test
    def espresso_version = '3.3.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    // Dagger
    androidTestImplementation "com.google.dagger:dagger:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"

}